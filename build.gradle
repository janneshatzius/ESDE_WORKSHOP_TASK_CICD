plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
}

group = 'workshop'
version = '1.0-SNAPSHOT'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
}

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn test
    violationRules {
        rule {
            limit {
                counter = 'LINE'      // align with Codecov
                value   = 'COVEREDRATIO'
                minimum = 0.90
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.register('exportPagesData', JavaExec) {
    group = "pages"
    description = "Write docs/data.json and docs/index.html"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'workshop.ExportPagesData'
    environment 'BUILD_VERSION', System.getenv('GITHUB_RUN_NUMBER') ?: 'local'
}
